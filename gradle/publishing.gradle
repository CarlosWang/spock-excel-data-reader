buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    }
}

apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['mavenJava']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload

    pkg {
        desc = 'Library for reading test data out of Excel to drive Spock tests.'
        repo = 'Grand-Test-Auto'
        name = 'spock-excel-data-reader'
        userOrg = 'testworx'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/testworx/spock-excel-data-reader.git'
        issueTrackerUrl = 'https://github.com/testworx/spock-excel-data-reader/issues'
        labels = ['spock', 'excel']
        publicDownloadNumbers = true


        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build.dependsOn sourcesJar
build.dependsOn javadocJar

apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}